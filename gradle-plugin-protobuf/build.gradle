println """\
Welcome to Gradle $gradle.gradleVersion - http://www.gradle.org
Gradle home is set to: $gradle.gradleHomeDir
Gradle user directory is set to: $gradle.gradleUserHomeDir

Base directory: $projectDir
Running script ${relativePath(buildFile)}
"""

apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'groovy'
apply plugin: 'maven'

apply plugin: de.huxhorn.gradle.pgp.PgpPlugin

group = 'ws.antonov.gradle.plugins'
version = '0.4'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'de.huxhorn.gradle:de.huxhorn.gradle.pgp-plugin:0.0.3'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    testCompile group: 'junit', name: 'junit', version: '4.8.1', "org.spockframework:spock-core:0.4-groovy-1.7@jar"
    groovy localGroovy()
}

task wrapper(type: Wrapper) {
    gradleVersion = '0.9.2'
}

pgp {
    secretKeyRingFile = new File("${System.properties['user.home']}/.gnupg/secring.gpg")
    // keyId: specify with -PpgpKeyId=
    // password: specify with -PpgpPassword=
}

task sourcesJar(type: Jar, dependsOn:classes) {
     classifier = 'sources'
     from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn:groovydoc) {
     classifier = 'groovydoc'
     from groovydoc.destinationDir
}

artifacts {
     archives sourcesJar
     archives groovydocJar
}

uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: "http://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: System.getProperty('nexusUser'), password: System.getProperty('nexusPassword'))
      }
      pom.project {
        name project.name
        description "Gradle build plugin to handle Protocol Buffers automated code generation and compilation"
        url "http://code.google.com/p/gradle-plugin-protobuf/"
        licenses {
          license {
            name "New BSD License"
            url "http://www.opensource.org/licenses/bsd-license.php"
          }
        }
        developers {
          developer {
            id "aantono"
            name "Alex Antonov"
            email "aantonov@orbitz.com"
          }
          developer {
            id "valkolovos"
            name "Val Kolovos"
            email "valkolovos@yahoo.com"
          }
          developer {
            id "mleinartas"
            name "Michael Leinartas"
            email "mleinartas@gmail.com"
          }
        }
        scm {
          connection "scm:svn:http://gradle-plugin-protobuf.googlecode.com/svn/trunk/"
          developerConnection "scm:svn:http://gradle-plugin-protobuf.googlecode.com/svn/trunk/"
          url "http://gradle-plugin-protobuf.googlecode.com/svn/trunk/"
        }
      }
    }
  }
}
