// A Java project that demonstrates most of the configuration features of this
// plugin.

apply plugin: 'java'
apply plugin: 'com.google.protobuf'

configurations {
    nanoCompile
    grpcCompile
}

sourceSets {
  nano {
    compileClasspath += configurations.nanoCompile
  }
  grpc {
    compileClasspath += configurations.grpcCompile
  }
  grpc_nano {
    compileClasspath += configurations.grpcCompile
  }
  test {
    compileClasspath += nano.output
    compileClasspath += grpc.output
    compileClasspath += grpc_nano.output
    runtimeClasspath += nano.output
    runtimeClasspath += grpc.output
    runtimeClasspath += grpc_nano.output
  }
}

def protobufDep = 'com.google.protobuf:protobuf-java:3.0.0-alpha-3'
def protobufNanoDep = 'com.google.protobuf.nano:protobuf-javanano:3.0.0-alpha-3'

dependencies {
    compile protobufDep
    protobuf files("lib/protos.tar.gz")
    //protobuf files("ext/test1.proto", "ext/test2.proto")
    protobuf fileTree("ext/") {
        //exclude 'more.proto'
    }
    testProtobuf files("lib/protos-test.tar.gz")
    testCompile 'junit:junit:4.7', protobufNanoDep
    nanoCompile protobufNanoDep
    grpcCompile protobufDep, protobufNanoDep,
        'io.grpc:grpc-stub:0.7.0', 'io.grpc:grpc-protobuf:0.7.0',
        'io.grpc:grpc-protobuf-nano:0.7.0'
}

protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.0.0-alpha-3'
  }
  plugins {
    grpc {
      artifact = 'io.grpc:protoc-gen-grpc-java:0.7.0'
    }
  }
  generateProtoTasks {
    ofSourceSet('nano')*.builtins {
      remove java
      javanano {
        option 'java_multiple_files=false'
      }
    }
    ofSourceSet('grpc')*.plugins {
      // No options. Note the braces cannot be omitted, otherwise the plugin
      // will not be added. This is because of the implicit way
      // NamedDomainObjectContainer binds the methods.
      grpc { }
    }
    ofSourceSet('grpc_nano').each { task ->
      task.builtins {
        remove java
        javanano {
          option 'ignore_services=true'
        }
      }
      task.plugins {
        grpc {
          option 'nano=true'
        }
      }
      task.generateDescriptorSet = true
    }
  }
}

// To include all sourceSets in the project jar
jar {
  sourceSets.all { sourceSet ->
    from sourceSet.output
    dependsOn sourceSet.getCompileTaskName('java')
  }
}

def assertJavaCompileHasProtoGeneratedDir(String sourceSet, Collection<String> codegenPlugins) {
  def compileJavaTask = tasks.getByName(sourceSets.getByName(sourceSet).getCompileTaskName("java"))
  assertJavaCompileHasProtoGeneratedDir(project, sourceSet, compileJavaTask, codegenPlugins)
}

def assertFileExists(boolean exists, String path) {
  if (exists) {
    org.junit.Assert.assertTrue("\"${path}\" exists", (path as File).exists());
  } else {
    org.junit.Assert.assertFalse("\"${path}\" doesn't exist", (path as File).exists());
  }
}

test.doLast {
  org.junit.Assert.assertEquals(
      ['generateProto', 'generateNanoProto', 'generateGrpcProto',
       'generateGrpc_nanoProto', 'generateTestProto'] as Set,
       protobuf.generateProtoTasks.all().collect({ it.name }) as Set)
  org.junit.Assert.assertEquals(
      ['generateProto'] as Set,
       protobuf.generateProtoTasks.ofSourceSet('main').collect({ it.name }) as Set)
  org.junit.Assert.assertEquals(
      ['generateNanoProto'] as Set,
       protobuf.generateProtoTasks.ofSourceSet('nano').collect({ it.name }) as Set)
  assertJavaCompileHasProtoGeneratedDir('main', ['java'])
  assertJavaCompileHasProtoGeneratedDir('test', ['java'])
  assertJavaCompileHasProtoGeneratedDir('nano', ['javanano'])
  assertJavaCompileHasProtoGeneratedDir('grpc', ['java', 'grpc'])
  assertJavaCompileHasProtoGeneratedDir('grpc_nano', ['javanano', 'grpc'])

  // Check generateDescriptorSet option has been honored
  ['main', 'test', 'nano', 'grpc'].each { sourceSet ->
    assertFileExists(false, "$buildDir/generated/source/proto/$sourceSet/descriptor_set.desc")
  }
  assertFileExists(true, "$buildDir/generated/source/proto/grpc_nano/descriptor_set.desc")
}
