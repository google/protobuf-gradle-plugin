// A Java project that demonstrates the customization of proto source
// directories through the sourceSet DSL.

apply plugin: 'java'
apply plugin: 'com.google.protobuf'

sourceSets {
  main {
    proto {
      // In addition to the default 'src/main/proto'
      srcDir 'src/main/protobuf'
      srcDir 'src/main/protocolbuffers'
      // In addition to '**/*.proto' (use with caution).
      // Using an extension other than 'proto' is NOT recommended, because when
      // proto files are published along with class files, we can only tell the
      // type of a file from its extension.
      include '**/*.protodevel'
    }
  }
  test {
    proto {
      // In addition to the default 'src/test/proto'
      srcDir 'src/test/protocolbuffers'
    }
  }
}

dependencies {
    compile 'com.google.protobuf:protobuf-java:3.0.0-beta-3'

    testCompile 'junit:junit:4.12'
}

protobuf.protoc {
  artifact = 'com.google.protobuf:protoc:3.0.0-beta-3'
}

task printDeps(dependsOn: build) << {
  configurations.each { println it }
  sourceSets.each { println it.getCompileTaskName("proto") }
  sourceSets.each { println it.getCompileTaskName("java") }
  sourceSets.each { println it }
  println tasks['compileJava'].source
  println project.buildDir
  println project.buildDir.path
}
