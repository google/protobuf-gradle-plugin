// A minimal example Java project that uses the protobuf plugin.
// To build it:
// $ ../gradlew clean build

apply plugin: 'java'
apply plugin: 'com.google.protobuf'

repositories {
  maven { url "https://plugins.gradle.org/m2/" }
}

buildscript {
  repositories {
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath 'com.google.protobuf:protobuf-gradle-plugin:0.7.7'
  }
}

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

dependencies {
  compile 'com.google.protobuf:protobuf-java:3.0.0-alpha-3'
  compile 'io.grpc:grpc-stub:0.7.0'
  compile 'io.grpc:grpc-protobuf:0.7.0'
  // Extra proto source files besides the ones residing under
  // "src/main".
  protobuf files("lib/protos.tar.gz")
  protobuf fileTree("ext/")

  testCompile 'junit:junit:4.12'
  // Extra proto source files for test besides the ones residing under
  // "src/test".
  testProtobuf files("lib/protos-test.tar.gz")
}

protobuf {
  protoc {
    // The artifact spec for the Protobuf Compiler
    artifact = 'com.google.protobuf:protoc:3.0.0-alpha-3'
  }
  plugins {
    // Optional: an artifact spec for a protoc plugin, with "grpc" as
    // the identifier, which can be referred to in the "plugins"
    // container of the "generateProtoTasks" closure.
    grpc {
      artifact = 'io.grpc:protoc-gen-grpc-java:0.7.0'
    }
  }
  generateProtoTasks {
    ofSourceSet('main')*.plugins {
      // Apply the "grpc" plugin whose spec is defined above, without options.
      // No options. Note the braces cannot be omitted, otherwise the plugin
      // will not be added. This is because of the implicit way
      // NamedDomainObjectContainer binds the methods.
      grpc { }
    }
  }
}
