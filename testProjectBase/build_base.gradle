// This build is not a complete project, but is used to generate a project.
// See: ProtobufPluginTestHelper.groovy

repositories {
    maven { url "https://plugins.gradle.org/m2/" }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

sourceSets {
  grpc {
  }
  test {
    compileClasspath += grpc.output
    runtimeClasspath += grpc.output
  }
}

def protobufDep = 'com.google.protobuf:protobuf-java:3.0.0'

dependencies {
  protobuf files("lib/protos.tar.gz")
  protobuf files("ext/")
  testProtobuf files("lib/protos-test.tar.gz")

  implementation protobufDep
  implementation 'javax.annotation:javax.annotation-api:1.3.2'
  testImplementation 'junit:junit:4.12'
  // KotlinFooTest.kt requires reflection utilities
  testImplementation "org.jetbrains.kotlin:kotlin-reflect:1.2.0"
  grpcImplementation protobufDep
  grpcImplementation 'io.grpc:grpc-stub:1.0.0-pre2'
  grpcImplementation 'io.grpc:grpc-protobuf:1.0.0-pre2'
  grpcImplementation 'javax.annotation:javax.annotation-api:1.3.2'
}

protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.0.0'
  }
  plugins {
    grpc {
      artifact = 'io.grpc:protoc-gen-grpc-java:1.0.0-pre2'
    }
  }
  generateProtoTasks {
    ofSourceSet('grpc') {
      plugins {
        grpc {
          outputSubDir = 'grpc_output'
        }
      }
      generateDescriptorSet = true
    }
  }
}

// To include all sourceSets in the project jar
jar {
  sourceSets.all { sourceSet ->
    from sourceSet.output
    dependsOn sourceSet.getCompileTaskName('java')
  }

  // gradle 7+ requires a duplicate strategy to be explicitly defined
  duplicatesStrategy(DuplicatesStrategy.INCLUDE)
}

def assertJavaCompileHasProtoGeneratedDir(String sourceSet, Collection<String> codegenPlugins) {
  def compileJavaTask = tasks.getByName(sourceSets.getByName(sourceSet).getCompileTaskName("java"))
  assertJavaCompileHasProtoGeneratedDir(project, sourceSet, compileJavaTask, codegenPlugins)
}

def assertFileExists(boolean exists, String path) {
  if (exists) {
    assert (path as File).exists()
  } else {
    assert !(path as File).exists()
  }
}

test.doLast {
  assert ['main', 'grpc', 'test'] as Set ==
    protobuf.generateProtoTasks.all().collect({ it.name }) as Set

  assert ['main'] as Set == protobuf.generateProtoTasks.ofSourceSet('main').collect({ it.name }) as Set
  assert [] as Set == protobuf.generateProtoTasks.ofTest().collect({ it.name }) as Set
  assert [] as Set == protobuf.generateProtoTasks.ofNonTest().collect({ it.name }) as Set
  assert [] as Set == protobuf.generateProtoTasks.ofFlavor('flavor-name').collect({ it.name }) as Set
  assert [] as Set == protobuf.generateProtoTasks.ofBuildType('buildType-name').collect({ it.name }) as Set
  assert [] as Set == protobuf.generateProtoTasks.ofVariant('variant-name').collect({ it.name }) as Set

  assertJavaCompileHasProtoGeneratedDir('main', ['java'])
  assertJavaCompileHasProtoGeneratedDir('test', ['java'])
  assertJavaCompileHasProtoGeneratedDir('grpc', ['java', 'grpc_output'])

  // Check generateDescriptorSet option has been honored
  ['main', 'test'].each { sourceSet ->
    assertFileExists(false, "$buildDir/generated/source/proto/$sourceSet/descriptor_set.desc")
  }
  assertFileExists(true, "$buildDir/generated/source/proto/grpc/descriptor_set.desc")
}

rootProject.ext {
  // Shared test utility. Checks a JavaCompile task for the given sourceSet
  // includes the generated source dirs for the given codegenPlugins, and does
  // not include any other dirs under the generated code base dir.
  assertJavaCompileHasProtoGeneratedDir = {
      Project project, String sourceSet, JavaCompile compileJavaTask, Collection<String> codegenPlugins ->
    def baseDir = "${project.buildDir}/generated/source/proto/$sourceSet" as File
    // The expected direct subdirectories under baseDir
    def expectedDirs = codegenPlugins.collect { codegenPlugin ->
      "${project.buildDir}/generated/source/proto/$sourceSet/$codegenPlugin" as File
    } as Set

    def actualDirs = new HashSet()
    compileJavaTask.source.visit { fileVisitDetails ->
      // If the visited file is or is under a direct subdirectory of baseDir, add
      // that subdirectory to actualDirs.
      def file = fileVisitDetails.file
      while (true) {
        if (file.parentFile == baseDir) {
          actualDirs.add file
        }
        if (file.parentFile == null) {
          break
        }
        file = file.parentFile
      }
    }
    assert expectedDirs == actualDirs
  }
}
